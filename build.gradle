plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

def mainVerticle = project.properties['mainVerticle'] ?: "com.kyra.portal.PortalVerticle"
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

group = 'com.kyra'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  vertxVersion = '3.9.1'
  junitJupiterEngineVersion = '5.6.0'
}

dependencies {
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-pg-client:$vertxVersion"
  implementation "io.vertx:vertx-service-discovery:$vertxVersion"
  implementation "io.vertx:vertx-redis-client:$vertxVersion"
  implementation "io.vertx:vertx-web-templ-handlebars:$vertxVersion"
  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}


subprojects {
  apply plugin: 'java'
  apply plugin: 'application'
  apply plugin: 'com.github.johnrengelman.shadow'

  repositories {
    mavenCentral()
  }

  project(":account") {
    dependencies {
      compile project(':common')
      compile "io.vertx:vertx-web-templ-handlebars:$vertxVersion"
    }
  }

  project(":portal") {
    apply plugin: 'java'

    dependencies {
      compile project(':common')
    }
  }

  project(":common") {
    apply plugin: 'java'

    dependencies {
      compile "io.vertx:vertx-web:$vertxVersion"
      compile "io.vertx:vertx-pg-client:$vertxVersion"
      compile "io.vertx:vertx-service-discovery:$vertxVersion"
      compile "io.vertx:vertx-redis-client:$vertxVersion"
    }
  }
}

allprojects {
  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  application {
    mainClassName = 'io.vertx.core.Launcher'
  }

  run {
    args = ["run",
            mainVerticle,
            "--redeploy=$watchForChange",
            "--launcher-class=$mainClassName",
            "--on-redeploy=$doOnChange",
            "--java-opts",
            "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8000",
            "-Dvertx.disableFileCaching=true"]
  }

  shadowJar {
    classifier = 'fat'
    manifest {
      attributes 'Main-Verticle': mainVerticle
    }
    mergeServiceFiles {
      include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
  }
}
